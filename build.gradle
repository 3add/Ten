plugins {
    id('java')
    id("xyz.jpenilla.run-paper") version("2.3.1")
    id("io.papermc.paperweight.userdev") version("2.0.0-beta.19")
    id("com.gradleup.shadow") version("9.2.2")
}

group = 'threeadd'
version = '1.0'

repositories {
    mavenCentral() // for HikariCP and Command API

    maven { // paper (obv), for paper dev bundle
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "enginehub-repo"
        url = "https://maven.enginehub.org/repo/"
    }

    maven { // Code MC for: packetevents
        name = "code-mc-repo"
        url = "https://repo.codemc.io/repository/maven-releases/"
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.11-R0.1-SNAPSHOT") // Paper + Internals
    implementation(platform("com.intellectualsites.bom:bom-newest:1.55"))
    implementation("com.zaxxer:HikariCP:7.0.2") // HikariCP

    compileOnly("com.github.retrooper:packetevents-spigot:2.9.5") // packetevents
    compileOnly("dev.jorel:commandapi-paper-core:11.1.0") // Command API
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")
}

shadowJar {
    archiveClassifier.set('')
    relocate('com.zaxxer.hikari', 'threeadd.ten.hikari')
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute(module("org.lz4:lz4-java"))
                    .using(module("at.yawk.lz4:lz4-java:1.8.1"))
                    .because("Paper provides lz4-java via at.yawk.lz4")
        }
    }
}

runServer {
    minecraftVersion("1.21.11")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
